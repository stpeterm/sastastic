class writer;                                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
write_to_file : method                                                                                                                                                                                                                                          
      obj  :i:list  /* the object/array that needs to be written                                          */                                                                                                                                                    
      typ  :i:char  /* type: either 'A' for array, or something else - alters brace behavior              */                                                                                                                                                    
      fid  :i:num   /* open file id that we're writing to */                                                                                                                                                                                                    
      optional=                                                                                                                                                                                                                                                 
      nm  :i:char   /* the name of said object - popl will destroy it, so it needs to be kept on the side */                                                                                                                                                    
      dlm2:i:char   /* keeping a delimiter on the side to minimize the number of submit statements        */                                                                                                                                                    
      spac:i:num    /* appears to work only if you decide to _not_ use actual spaces? ugh, SAS            */                                                                                                                                                    
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      dcl num i rc;                                                                                                                                                                                                                                             
      dcl char i_name i_type;                                                                                                                                                                                                                                   
      dcl char(32767) cval;                                                                                                                                                                                                                                     
      dcl char(32767) to_write;                                                                                                                                                                                                                                 
      dcl char(1) brace_open brace_close dlm ;                                                                                                                                                                                                                  
      dcl char spaces = '';                                                                                                                                                                                                                                     
                                                                                                                                                                                                                                                                
      /* do some nice formatting stuff - actually, this is unnecessary and could be removed in production, but */                                                                                                                                               
      /* it sure is nice for debuggin - it makes the output so much easier to read                             */                                                                                                                                               
      control asis;                                                                                                                                                                                                                                             
      do i = 1 to spac;                                                                                                                                                                                                                                         
         spaces = cat(spaces,' ');                                                                                                                                                                                                                              
      end;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
      * first, get the type of brace - it depends on whether we are trying to write an ;                                                                                                                                                                        
      * object or an array                                                             ;                                                                                                                                                                        
      if typ = 'A' then do; brace_open  = '['; brace_close = ']'; end;                                                                                                                                                                                          
      else do;              brace_open  = '{'; brace_close = '}'; end;                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
      * if a name was specified, write it out. otherwise, just the open brace          ;                                                                                                                                                                        
      if nm ne '' then fput(fid,catt(' ',spaces,catt('"',nm,'"'),':',brace_open));                                                                                                                                                                              
      else             fput(fid,catt(' ',spaces,brace_open));                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
      * now, parse through the object and write stuff out                              ;                                                                                                                                                                        
      dcl list subl = makelist();                                                                                                                                                                                                                               
      do while(listlen(obj));                                                                                                                                                                                                                                   
         * if we're on the last item in the object, don't include the trailing comma   ;                                                                                                                                                                        
         * if there are still items to go, leave the comma in                          ;                                                                                                                                                                        
         if (listlen(obj)-1) then dlm=','; else dlm='';                                                                                                                                                                                                         
         i_name = nameitem(obj);                                                                                                                                                                                                                                
         i_type = itemtype(obj);                                                                                                                                                                                                                                
         * how the item is processed depends on what type it is                        ;                                                                                                                                                                        
         select(i_type);                                                                                                                                                                                                                                        
            when('C') do;                                                                                                                                                                                                                                       
               cval = popc(obj);                                                                                                                                                                                                                                
               * do some escaping to match the JSON spec ;                                                                                                                                                                                                      
               cval = tranwrd(cval,'\','\\');     * reverse solidus;                                                                                                                                                                                            
               cval = tranwrd(cval,'/','\/');     * solidus (snaaaaaake);                                                                                                                                                                                       
               cval = tranwrd(cval,'09'x,'\t');   * horizontal tab;                                                                                                                                                                                             
               cval = tranwrd(cval,'0D'x,'\r');   * carriage return;                                                                                                                                                                                            
               cval = tranwrd(cval,'0A'x,'\n');   * line feed;                                                                                                                                                                                                  
               cval = tranwrd(cval,'0C'x,'\f');   * form feed;                                                                                                                                                                                                  
               cval = tranwrd(cval,'08'x,'\b');   * backspace;                                                                                                                                                                                                  
               cval = tranwrd(cval,'00'x,'');     * null;                                                                                                                                                                                                       
               cval = tranwrd(cval,'"','\"');     * quotation mark;                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
               * wrap the character in quotes, unless true/false/null ;                                                                                                                                                                                         
               if cval not in ('_TRUE_','_FALSE_','_NULL_') then cval = catt('"',cval,'"');                                                                                                                                                                     
               else do;                                                                                                                                                                                                                                         
                  if cval = '_TRUE_' then cval = 'true';                                                                                                                                                                                                        
                  else if cval = '_FALSE_' then cval = 'false';                                                                                                                                                                                                 
                  else if cval = '_NULL_' then cval = 'null';                                                                                                                                                                                                   
               end;                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
               * what gets written out depends on two things: if there is a name, and  ;                                                                                                                                                                        
               * if we're not trying to write an array                                 ;                                                                                                                                                                        
               to_write = ifc(                                                                                                                                                                                                                                  
                  i_name ne '' and typ ne 'A',                                                                                                                                                                                                                  
                  catt(spaces,catt('"',i_name,'"'),':',cval,dlm),                                                                                                                                                                                               
                  catt(spaces,cval,dlm)                                                                                                                                                                                                                         
               );                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
               * and, SAS's quote function will double any existing double-quotes that ;                                                                                                                                                                        
               * are already in the argument - so why not some MORE processing?        ;                                                                                                                                                                        
               *to_write = tranwrd(to_write,'""','"'); * SO UNREADABLE ;                                                                                                                                                                                        
               fput(fid,to_write);                                                                                                                                                                                                                              
            end;                                                                                                                                                                                                                                                
            * cheating here by converting a number to a string with the = assignment   ;                                                                                                                                                                        
            * SAS will coerce the numeric returned to popn to a string as cval is      ;                                                                                                                                                                        
            * already defined to be a string                                           ;                                                                                                                                                                        
            when('N') do;                                                                                                                                                                                                                                       
               cval = popn(obj);                                                                                                                                                                                                                                
               * another kludge - assume that missing numerics are JSON type NULL      ;                                                                                                                                                                        
               if cval = '.' then cval = 'null';                                                                                                                                                                                                                
               to_write = ifc(                                                                                                                                                                                                                                  
                  i_name ne '' and typ ne 'A',                                                                                                                                                                                                                  
                  catt(spaces,catt('"',i_name,'"'),':',cval,dlm),                                                                                                                                                                                               
                  catt(spaces,cval,dlm)                                                                                                                                                                                                                         
               );                                                                                                                                                                                                                                               
               fput(fid,to_write);                                                                                                                                                                                                                              
            end;                                                                                                                                                                                                                                                
            * nested object or array - the kludge is that if the list name starts with ;                                                                                                                                                                        
            * the literal '_ARRAY_', it's a JSON array                                 ;                                                                                                                                                                        
            when('L') do;                                                                                                                                                                                                                                       
               subl = popl(obj);                                                                                                                                                                                                                                
               * if you're currently in the middle of an array, you have to crush      ;                                                                                                                                                                        
               * the name that is currently assigned before moving down the hierarchy  ;                                                                                                                                                                        
               if typ = 'A' then i_name = '';                                                                                                                                                                                                                   
               if substr(i_name,1,7) = '_ARRAY_' then do; write_to_file(subl,'A',fid,substr(i_name,9),dlm,spac+1); end;                                                                                                                                         
               else do;                                   write_to_file(subl,'O',fid,       i_name   ,dlm,spac+1); end;                                                                                                                                         
               rc = dellist(subl);                                                                                                                                                                                                                              
            end;                                                                                                                                                                                                                                                
            otherwise; /* don't end up here! */                                                                                                                                                                                                                 
         end;                                                                                                                                                                                                                                                   
      end;                                                                                                                                                                                                                                                      
fput(fid,catt(brace_close,dlm2));                                                                                                                                                                                                                               
rc = fwrite(fid);                                                                                                                                                                                                                                               
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
write : method                                                                                                                                                                                                                                                  
      json:i:list                                                                                                                                                                                                                                               
      fref:i:char                                                                                                                                                                                                                                               
      optional=output_type:i:char                                                                                                                                                                                                                               
      ;                                                                                                                                                                                                                                                         
      control asis;                                                                                                                                                                                                                                             
      dcl list input = copylist(json,'y');                                                                                                                                                                                                                      
      dcl num rc;                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
      if lowcase(fref) = '_webout' then do;                                                                                                                                                                                                                     
         submit continue;                                                                                                                                                                                                                                       
            %let appsrv_rc  = %sysfunc(appsrv_header(content-type,application/json));                                                                                                                                                                           
         endsubmit;                                                                                                                                                                                                                                             
      end;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
      dcl num fid = fopen(fref,'O', 32767);                                                                                                                                                                                                                     
      if upcase(first(output_type)) = 'A' then                                                                                                                                                                                                                  
           write_to_file(input,'A',fid);                                                                                                                                                                                                                        
      else write_to_file(input,'O',fid);                                                                                                                                                                                                                        
      rc = dellist(input);                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
      *rc = fwrite(fid);                                                                                                                                                                                                                                        
      rc = fclose(fid);                                                                                                                                                                                                                                         
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
serialize : method                                                                                                                                                                                                                                              
      json:i:list                                                                                                                                                                                                                                               
      return=char;                                                                                                                                                                                                                                              
      dcl char(32767) this out;                                                                                                                                                                                                                                 
                                                                                                                                                                                                                                                                
      dcl num rc fid;                                                                                                                                                                                                                                           
      control asis;                                                                                                                                                                                                                                             
                                                                                                                                                                                                                                                                
      * write to a temporary file, then read it back in and return what you've got;                                                                                                                                                                             
      rc = filename('_jsonout','','temp');                                                                                                                                                                                                                      
      write(json,'_jsonout');                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
      fid = fopen('_jsonout','i',32767);                                                                                                                                                                                                                        
      rc = fsep(fid,'');                                                                                                                                                                                                                                        
      do while(fread(fid) = 0);                                                                                                                                                                                                                                 
      rc  = fget(fid,this);                                                                                                                                                                                                                                     
      out = out || this;                                                                                                                                                                                                                                        
      end;                                                                                                                                                                                                                                                      
      rc  = fclose(fid);                                                                                                                                                                                                                                        
      rc  = fdelete('_jsonout');                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
return(out);                                                                                                                                                                                                                                                    
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
endclass;
