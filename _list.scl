import sashelp.classes;                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
class _list;                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
private char name           / (state='O', initialValue = '_LIST');                                                                                                                                                                                              
public  char renameMethod   / (initialValue = 'FILTER', validValues='FILTER LEAVE NONE');                                                                                                                                                                       
private char datetime_regex / (initialValue = '/(DATE|TIME|HHMM|ANYDT|8601)/');                                                                                                                                                                                 
                                                                                                                                                                                                                                                                
make : method                                                                                                                                                                                                                                                   
      return = list;                                                                                                                                                                                                                                            
      dcl num rc;                                                                                                                                                                                                                                               
      dcl list outlist = makelist(); rc=setlattr(outlist,'honorcase');                                                                                                                                                                                          
return outlist;                                                                                                                                                                                                                                                 
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
make : method                                                                                                                                                                                                                                                   
      slist : i : char                                                                                                                                                                                                                                          
      return = list;                                                                                                                                                                                                                                            
      dcl num rc;                                                                                                                                                                                                                                               
      dcl list outlist = makelist(); rc=setlattr(outlist,'honorcase');                                                                                                                                                                                          
      rc = fillist('catalog',slist,outlist);                                                                                                                                                                                                                    
return outlist;                                                                                                                                                                                                                                                 
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
transform : method                                                                                                                                                                                                                                              
      list_in : i : list                                                                                                                                                                                                                                        
      trans   : i : list                                                                                                                                                                                                                                        
      return = list                                                                                                                                                                                                                                             
      / (forward = 'Y')                                                                                                                                                                                                                                         
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
rename : method                                                                                                                                                                                                                                                 
      rf : i : list /* rename from */                                                                                                                                                                                                                           
      r  : i : list /* rename parameters */                                                                                                                                                                                                                     
      optional =                                                                                                                                                                                                                                                
         filter : i : char                                                                                                                                                                                                                                      
      return=list   /* rename to */                                                                                                                                                                                                                             
      / (forward = 'Y')                                                                                                                                                                                                                                         
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
create_list : method                                                                                                                                                                                                                                            
      /* creates an list of lists */                                                                                                                                                                                                                            
      ds : i : char                                                                                                                                                                                                                                             
      optional=                                                                                                                                                                                                                                                 
         rn     : i : list                                                                                                                                                                                                                                      
         tr     : i : list                                                                                                                                                                                                                                      
      return=list;                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
      dcl num rc j nvars dsid;                                                                                                                                                                                                                                  
      dcl char var_nm;                                                                                                                                                                                                                                          
      dcl char id_nm = '';                                                                                                                                                                                                                                      
      dcl list outlist = makelist(); rc=setlattr(outlist,'honorcase');                                                                                                                                                                                          
      dcl list sublist = makelist(); rc=setlattr(sublist,'honorcase');                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
      dsid = open(ds);                                                                                                                                                                                                                                          
      nvars = attrn(dsid,'nvars');                                                                                                                                                                                                                              
      rc = fetch(dsid);                                                                                                                                                                                                                                         
      if rc = 0 then do while(rc=0);                                                                                                                                                                                                                            
         rc = clearlist(sublist);                                                                                                                                                                                                                               
         do j = 1 to nvars;                                                                                                                                                                                                                                     
            var_nm = varname(dsid,j);                                                                                                                                                                                                                           
            /* the assumption is that date and time formats will be sent as their formatted values; and all */                                                                                                                                                  
            /* character formats will be transmitted as their formatted values in character. purely numeric */                                                                                                                                                  
            /* fields will be transmitted as numbers                                                        */                                                                                                                                                  
            /* I'm sure there are some oddball date and time formats that aren't represented in the regex   */                                                                                                                                                  
            /* below, but the current assumption is that they never get used                                */                                                                                                                                                  
                                                                                                                                                                                                                                                                
            if vartype(dsid,j) = 'N' then do;                                                                                                                                                                                                                   
               * If it's not a date or time, just insert the numeric value;                                                                                                                                                                                     
               if prxmatch(datetime_regex,varfmt(dsid,j)) = 0 then do;                                                                                                                                                                                          
                  rc = insertn(sublist,getvarn(dsid,j),-1,var_nm);                                                                                                                                                                                              
               end;                                                                                                                                                                                                                                             
               * If it's a date but it's empty, insert an empty string;                                                                                                                                                                                         
               else if getvarn(dsid,j) = . then do;                                                                                                                                                                                                             
                  rc = insertc(sublist, "", -1, var_nm);                                                                                                                                                                                                        
               end;                                                                                                                                                                                                                                             
               * Otherwise, insert the formatted string;                                                                                                                                                                                                        
               else do;                                                                                                                                                                                                                                         
                  rc = insertc(sublist,getvarf(dsid,j),-1,var_nm);                                                                                                                                                                                              
               end;                                                                                                                                                                                                                                             
            end;                                                                                                                                                                                                                                                
            else do;                                                                                                                                                                                                                                            
               rc = insertc(sublist,getvarf(dsid,j),-1,var_nm);                                                                                                                                                                                                 
            end;                                                                                                                                                                                                                                                
         end;                                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
         if listlen(rn) > 0 then sublist = _self_.rename(sublist,rn);                                                                                                                                                                                           
         /* if an id is specified, (pull it from the sublist? or no?) and use the identifier as the list name */                                                                                                                                                
         rc = insertl(outlist,copylist(sublist),-1,id_nm);                                                                                                                                                                                                      
         rc = fetch(dsid);                                                                                                                                                                                                                                      
      end;                                                                                                                                                                                                                                                      
      rc = dellist(sublist);                                                                                                                                                                                                                                    
      rc = close(dsid);                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      if ^cmiss(tr) then outlist = transform(outlist,tr);                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
return(outlist);                                                                                                                                                                                                                                                
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
create_list : method                                                                                                                                                                                                                                            
      /* creates an list of lists */                                                                                                                                                                                                                            
      dsid : i : num                                                                                                                                                                                                                                            
      optional=                                                                                                                                                                                                                                                 
         rn     : i : list                                                                                                                                                                                                                                      
         tr     : i : list                                                                                                                                                                                                                                      
      return=list;                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
      return(create_list(dsname(dsid),rn,tr));                                                                                                                                                                                                                  
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
create_kav : method                                                                                                                                                                                                                                             
      ds : i : char                                                                                                                                                                                                                                             
      ls : i : list                                                                                                                                                                                                                                             
      optional=                                                                                                                                                                                                                                                 
         tr : i : list                                                                                                                                                                                                                                          
      return=list;                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
      dcl num dsid rc keynum attrnum valnum;                                                                                                                                                                                                                    
      dcl char keyname attrname valname;                                                                                                                                                                                                                        
      dcl char keytype attrtype valtype;                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                
      dcl list outlist = makelist();                                                                                                                                                                                                                            
      dcl list blanklist = makelist();   * always handy! ;                                                                                                                                                                                                      
      dcl list sublist;                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      dsid = open(ds);                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
      dcl char key  = getnitemc(ls,'key',1,1,'_nokey_');                                                                                                                                                                                                        
      dcl char attr = getnitemc(ls,'attr');                                                                                                                                                                                                                     
      dcl char val  = getnitemc(ls,'val');                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
      keynum   = varnum(dsid,key);                                                                                                                                                                                                                              
      attrnum  = varnum(dsid,attr);                                                                                                                                                                                                                             
      valnum   = varnum(dsid,val);                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                
      rc = fetch(dsid);                                                                                                                                                                                                                                         
      if rc = 0 then do while(rc=0);                                                                                                                                                                                                                            
         * get the formatted value of the key, then check to see if it already exists in the output list ;                                                                                                                                                      
         if keynum then do;                                                                                                                                                                                                                                     
            keyname  = getvarf(dsid,keynum);                                                                                                                                                                                                                    
            *put keyname=;                                                                                                                                                                                                                                      
            if ^nameditem(outlist,keyname) then rc = insertl(outlist,makelist(),-1,keyname);                                                                                                                                                                    
            sublist = getiteml(outlist,nameditem(outlist,keyname));                                                                                                                                                                                             
         end;                                                                                                                                                                                                                                                   
         else do;                                                                                                                                                                                                                                               
            sublist = outlist;                                                                                                                                                                                                                                  
         end;                                                                                                                                                                                                                                                   
         attrname = getvarf(dsid,attrnum);                                                                                                                                                                                                                      
         *put attrname=;                                                                                                                                                                                                                                        
         select(vartype(dsid,valnum));                                                                                                                                                                                                                          
            * only have to worry about 'C' and 'N' here since we're in dataset land ;                                                                                                                                                                           
            when('C') rc = insertc(sublist,getvarc(dsid,valnum),-1,attrname);                                                                                                                                                                                   
            when('N') rc = insertn(sublist,getvarn(dsid,valnum),-1,attrname);                                                                                                                                                                                   
         end;                                                                                                                                                                                                                                                   
         rc = fetch(dsid);                                                                                                                                                                                                                                      
      end;                                                                                                                                                                                                                                                      
      rc = close(dsid);                                                                                                                                                                                                                                         
return(outlist);                                                                                                                                                                                                                                                
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
transform : method                                                                                                                                                                                                                                              
      list_in : i : list                                                                                                                                                                                                                                        
      trans   : i : list                                                                                                                                                                                                                                        
      return = list                                                                                                                                                                                                                                             
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      dcl list thisrow;                                                                                                                                                                                                                                         
      dcl list listout = makelist(); setlattr(listout,'honorcase');                                                                                                                                                                                             
      dcl list listref listrefcopy;                                                                                                                                                                                                                             
      dcl list poplist = makelist(); setlattr(listout,'honorcase');                                                                                                                                                                                             
      * need to keep a blank list with the honorcase attribute handy - insert copies where you *;                                                                                                                                                               
      * normally insert a makelist() directly                                                  *;                                                                                                                                                               
      dcl list blanklist = makelist(); setlattr(blanklist,'honorcase');                                                                                                                                                                                         
      dcl char thisrow_typ thisrow_val itemname;                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
      dcl num i j rc thisrow_idx;                                                                                                                                                                                                                               
      * take a list of lists and return a different list of lists, according to the transform *;                                                                                                                                                                
      do while(listlen(list_in));                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
         * throw a reference to the output list in - we'll move down as necessary *;                                                                                                                                                                            
         listref = listout;                                                                                                                                                                                                                                     
                                                                                                                                                                                                                                                                
         * get the current row from the input list *;                                                                                                                                                                                                           
         thisrow = popl(list_in);                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                
         * now, we're going to figure out where this row goes - if we need to, we'll create *;                                                                                                                                                                  
         * the necessary list structure                                                     *;                                                                                                                                                                  
         do i = 1 to listlen(trans);                                               * for every item in the transformation list     *;                                                                                                                           
            itemname = getitemc(trans,i);                                          * get the current item                          *;                                                                                                                           
            thisrow_idx = nameditem(thisrow,itemname);                             * and use it to look up the corresponding named *;                                                                                                                           
                                                                                   * item in the input list                        *;                                                                                                                           
            thisrow_typ = itemtype(thisrow,nameditem(thisrow,getitemc(trans,i)));                                                                                                                                                                               
            select(thisrow_typ);                                                                                                                                                                                                                                
               * the neverending internal debate - getitem or pop ? *;                                                                                                                                                                                          
               when('C') thisrow_val = /*pop*/getitemc(thisrow,thisrow_idx);                                                                                                                                                                                    
               when('N') thisrow_val = /*pop*/getitemn(thisrow,thisrow_idx);                                                                                                                                                                                    
               otherwise;                                                                                                                                                                                                                                       
            end;                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
            * if you get super-nested, there's no need to carry a bunch of redundant stuff. delete it *;                                                                                                                                                        
            do j = 1 to i;                                                                                                                                                                                                                                      
               dcl char temp=getitemc(trans,j);                                                                                                                                                                                                                 
               *put 'deleting from thisrow' temp=;                                                                                                                                                                                                              
               rc = delnitem(thisrow,getitemc(trans,j));                                                                                                                                                                                                        
            end;                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
            * if the transformation list item is named, insert a list with that name *;                                                                                                                                                                         
            if nameitem(trans,i) ne '' then do;                                                                                                                                                                                                                 
               if ^(nameditem(listref,nameitem(trans,i))) then rc = insertl(listref,copylist(blanklist),-1,nameitem(trans,i));                                                                                                                                  
               listref = getiteml(listref);                                                                                                                                                                                                                     
            end;                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
            if ^(nameditem(listref,thisrow_val)) then rc = insertl(listref,copylist(blanklist),-1,thisrow_val);                                                                                                                                                 
            listref = getniteml(listref,thisrow_val);                                                                                                                                                                                                           
         end;                                                                                                                                                                                                                                                   
                                                                                                                                                                                                                                                                
         * now that we have the place we're going to point everything to, we'll just unload the remainder of the list *;                                                                                                                                        
         * MSP 2014-12-16 - if you have multiple rows going in to the same place, they all kinda get stripped down in a 'bad' way ;                                                                                                                             
         * update to test the length of the list before unloading.;                                                                                                                                                                                             
         if listlen(listref) = 0 then do while(listlen(thisrow));                                                                                                                                                                                               
            itemname = nameitem(thisrow);                                                                                                                                                                                                                       
            select(itemtype(thisrow));                                                                                                                                                                                                                          
               when('C') rc = insertc(listref,popc(thisrow),-1,itemname);                                                                                                                                                                                       
               when('N') rc = insertn(listref,popn(thisrow),-1,itemname);                                                                                                                                                                                       
               * hope you never hit this ! *;                                                                                                                                                                                                                   
               when('L') rc = insertl(listref,popl(thisrow),-1,itemname);                                                                                                                                                                                       
               otherwise; * hope you never hit this either ! *;                                                                                                                                                                                                 
            end;                                                                                                                                                                                                                                                
         end;                                                                                                                                                                                                                                                   
         else do;                                                                                                                                                                                                                                               
            if itemtype(listref) ne 'L' then do;                                                                                                                                                                                                                
               listrefcopy = copylist(listref);                                                                                                                                                                                                                 
               rc = clearlist(listref);                                                                                                                                                                                                                         
               rc = insertl(listref,listrefcopy,-1);                                                                                                                                                                                                            
            end;                                                                                                                                                                                                                                                
            rc = insertl(listref,copylist(thisrow,'y'));                                                                                                                                                                                                        
         end;                                                                                                                                                                                                                                                   
      end;                                                                                                                                                                                                                                                      
return(listout);                                                                                                                                                                                                                                                
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
                                                                                                                                                                                                                                                                
rename : method                                                                                                                                                                                                                                                 
      rf : i : list /* rename from */                                                                                                                                                                                                                           
      r  : i : list /* rename parameters */                                                                                                                                                                                                                     
      optional =                                                                                                                                                                                                                                                
         method : i : char                                                                                                                                                                                                                                      
      return=list   /* rename to */                                                                                                                                                                                                                             
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      * if no method is specified, default to whatever the class has specified *;                                                                                                                                                                               
      if cmiss(method) then method = _self_.renameMethod;                                                                                                                                                                                                       
                                                                                                                                                                                                                                                                
      dcl char itemname itemnewname;                                                                                                                                                                                                                            
      dcl num  i rc;                                                                                                                                                                                                                                            
      dcl list outlist  = makelist(); rc=setlattr(outlist,'honorcase');                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      do i = 1 to listlen(rf);                                                                                                                                                                                                                                  
         itemname    = nameitem(rf,i);                                                                                                                                                                                                                          
         itemnewname = getnitemc(r,itemname,1,1,'_NONAME_');                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                
         * if filtering is in place, move to the next position, otherwise, set the new item name to the old item name *;                                                                                                                                        
         if      itemnewname = '_NONAME_' and method = 'FILTER' then continue;                                                                                                                                                                                  
         else if itemnewname = '_NONAME_' and method = 'LEAVE'  then itemnewname = itemname;                                                                                                                                                                    
         select(itemtype(rf,i));                                                                                                                                                                                                                                
            when('C') rc = insertc(outlist,getitemc(rf,i),-1,itemnewname);                                                                                                                                                                                      
            when('N') rc = insertn(outlist,getitemn(rf,i),-1,itemnewname);                                                                                                                                                                                      
            * these are gonna have some wacky effects, I bet *;                                                                                                                                                                                                 
            when('L') rc = insertl(outlist,getiteml(rf,i),-1,itemnewname);                                                                                                                                                                                      
            when('O') rc = inserto(outlist,getitemo(rf,i),-1,itemnewname);                                                                                                                                                                                      
         end;                                                                                                                                                                                                                                                   
      end;                                                                                                                                                                                                                                                      
return(outlist);                                                                                                                                                                                                                                                
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
/* nonrecursive */                                                                                                                                                                                                                                              
extract : method                                                                                                                                                                                                                                                
      ef : i : list /* extract from */                                                                                                                                                                                                                          
      e  : i : list /* extraction   */                                                                                                                                                                                                                          
      optional=                                                                                                                                                                                                                                                 
         tr : i : list                                                                                                                                                                                                                                          
      return=list                                                                                                                                                                                                                                               
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      dcl char itemname;                                                                                                                                                                                                                                        
      dcl num  i rc idx;                                                                                                                                                                                                                                        
      dcl list outlist  = makelist(); rc=setlattr(outlist,'honorcase');                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      do while(listlen(e));                                                                                                                                                                                                                                     
         itemname = popc(e);                                                                                                                                                                                                                                    
         idx = nameditem(ef,itemname);                                                                                                                                                                                                                          
         * if the named item can't be found, skip it *;                                                                                                                                                                                                         
         if idx = 0 then continue;                                                                                                                                                                                                                              
         select(itemtype(ef,idx));                                                                                                                                                                                                                              
            when('C') rc = insertc(outlist,getitemc(ef,idx),-1,itemname);                                                                                                                                                                                       
            when('N') rc = insertn(outlist,getitemn(ef,idx),-1,itemname);                                                                                                                                                                                       
            when('L') rc = insertl(outlist,getiteml(ef,idx),-1,itemname);                                                                                                                                                                                       
            when('O') rc = inserto(outlist,getitemo(ef,idx),-1,itemname);                                                                                                                                                                                       
         end;                                                                                                                                                                                                                                                   
      end;                                                                                                                                                                                                                                                      
      rc = dellist(e);                                                                                                                                                                                                                                          
                                                                                                                                                                                                                                                                
      if ^cmiss(tr) then do;                              * if there's a transform specified, *;                                                                                                                                                                
         dcl list sublist = copylist(outlist,'y');        * wrap everything in a new list     *;                                                                                                                                                                
         rc = clearlist(outlist);                         * ...                               *;                                                                                                                                                                
         rc = insertl(outlist,copylist(sublist,'y'));     * ...                               *;                                                                                                                                                                
         rc = dellist(sublist);                           * ...                               *;                                                                                                                                                                
         outlist = transform(outlist,tr);                 * and transform                     *;                                                                                                                                                                
      end;                                                                                                                                                                                                                                                      
return(outlist);                                                                                                                                                                                                                                                
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
deleteItems : method                                                                                                                                                                                                                                            
      df : i : list /* delete from */                                                                                                                                                                                                                           
      d  : i : list /* delete      */                                                                                                                                                                                                                           
      return=list                                                                                                                                                                                                                                               
      ;                                                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      dcl char itemname;                                                                                                                                                                                                                                        
      dcl num  i rc;                                                                                                                                                                                                                                            
      dcl list outlist  = copylist(df);                                                                                                                                                                                                                         
                                                                                                                                                                                                                                                                
      do while(listlen(d));                                                                                                                                                                                                                                     
         itemname = popc(d);                                                                                                                                                                                                                                    
         outlist = delnitem(outlist,itemname);                                                                                                                                                                                                                  
      end;                                                                                                                                                                                                                                                      
      rc = dellist(d);                                                                                                                                                                                                                                          
return(outlist);                                                                                                                                                                                                                                                
endmethod;                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                
endclass;
